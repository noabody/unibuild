# Maintainer: Noa Body <noabody@archlinux.org>
# git clone --depth 1 https://aur.archlinux.org/xpad-dkms-git.git ~/Dev/xpad-dkms-git
# cp ~/Dev/unibuild/data/arch/xpad-dkms.pkgbuild ~/Dev/xpad-dkms-git/PKGBUILD
# cd ~/Dev/xpad-dkms-git && makepkg -sf

_pkgname='xpad'
pkgname='xpad-dkms-git'
pkgver='0.4'
pkgrel='2'
pkgdesc="Driver for the Xbox/ Xbox 360/ Xbox 360 Wireless/ Xbox One Controllers"
arch=('i686' 'x86_64')
url="https://github.com/paroj/xpad"
license=('GPL2')
install="${pkgname}.install"
depends=('dkms')
makedepends=('git')
conflicts=("${_pkgname}-dkms")
source=("${pkgname}::git+https://github.com/paroj/xpad.git"
        "xpad.conf"
        "${pkgname}.install")
md5sums=('SKIP'
         '4218c9543d551377825392295544c3c2'
         '75cad51dc48d8fa879f926432beabf66')

pkgver() {
  cd "$pkgname"
  printf "0.4.%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short=7 HEAD)"
}

prepare () {
  perl -pi -e 's|(.*0x2e24, 0x0652.*)|\1\n\t{ 0x2f24, 0x011e, "PXN 008 Arcade Fightstick", 0, XTYPE_XBOX360 },\n\t{ 0x2f24, 0x0121, "PXN 008 X-Box One pad", 0, XTYPE_XBOXONE },|gi ; s|(.*VENDOR\(0x2f2.*)|\1\n\tXPAD_XBOXONE_VENDOR(0x2f24),		/* GameSir Controllers */|gi' ${pkgname}/xpad.c
}

package() {
    # install depmod config file so our driver gets higher priority than the intree module
    install -dm 755 "$pkgdir/etc/depmod.d"
    install -m 644 "$srcdir/xpad.conf" "$pkgdir/etc/depmod.d"

    cd "$srcdir/$pkgname"
    install -dm 755 "${pkgdir}/usr/src/${_pkgname}-${pkgver}"
    install -m 644 -T xpad.c "${pkgdir}/usr/src/${_pkgname}-${pkgver}/xpad.c"
    install -m 644 -T Makefile "${pkgdir}/usr/src/${_pkgname}-${pkgver}/Makefile"
    install -m 644 -T dkms.conf "${pkgdir}/usr/src/${_pkgname}-${pkgver}/dkms.conf"
}
