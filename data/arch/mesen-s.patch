diff a/mesen-s/PKGBUILD b/mesen-s/PKGBUILD
--- a/mesen-s/PKGBUILD  1969-12-31 17:00:00.000000000 -0700
+++ b/mesen-s/PKGBUILD  1969-12-31 17:00:00.000000000 -0700
@@ -0,0 +1,47 @@
+# Maintainer: Noa Body <noabody@archlinux.org>
+
+pkgname=mesen-s
+pkgver=0.4.0.r192.g7af0bbc
+pkgrel=1
+pkgdesc='A cross-platform NES/Famicom emulator'
+arch=(x86_64)
+makedepends=(clang gendesk zip mono-msbuild)
+depends=(mono sdl2)
+url='https://github.com/NovaSquirrel/Mesen-S'
+conflicts=("$pkgname"-bin "$pkgname"-git)
+license=(GPL3)
+source=($pkgname::git+https://github.com/NovaSquirrel/Mesen-SX.git)
+sha256sums=('SKIP')
+
+pkgver() {
+  cd $pkgname
+  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
+}
+
+prepare() {
+  cd $pkgname
+  make clean
+  # Prevent duplicate .desktop from getting created
+  sed -i 's/CreateShortcutFile(desktopFile, mimeTypes);//' UI/Config/FileAssociationHelper.cs
+  gendesk -f --pkgname Mesen-S --pkgdesc "$pkgdesc" --exec "/usr/bin/$pkgname" -n
+  # Invoke using mono in a wrapper, since wine (if installed) would open it otherwise
+  cat > "$pkgname" << EOF
+#!/bin/sh
+/usr/bin/mono /usr/share/$pkgname/Mesen-S "\$@"
+EOF
+}
+
+build() {
+  cd $pkgname
+  make -j$(nproc)
+}
+
+package() {
+  install -dm 755 "$pkgdir"/usr/{bin,share/{"$pkgname",applications,pixmaps}}
+  cd $pkgname
+  install -Dm 755 "$pkgname" "${pkgdir}"/usr/bin/
+  install -Dm 644 Mesen-S.desktop "${pkgdir}"/usr/share/applications/
+  install -Dm 644 UI/Resources/MesenSIcon.png "${pkgdir}"/usr/share/pixmaps/Mesen-S.png
+  install -Dm 755 bin/x64/Release/Mesen-S.exe "${pkgdir}"/usr/share/"$pkgname"/Mesen-S
+  install -Dm 644 InteropDLL/obj.x64/libMesenSCore.x64.dll "${pkgdir}"/usr/lib/libMesenSCore.dll
+}
