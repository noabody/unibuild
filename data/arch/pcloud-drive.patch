diff a/pcloud-drive/PKGBUILD b/pcloud-drive/PKGBUILD
--- a/pcloud-drive/PKGBUILD	1969-12-31 17:00:00.000000000 -0700
+++ b/pcloud-drive/PKGBUILD	1969-12-31 17:00:00.000000000 -0700
@@ -0,0 +1,41 @@
+# Maintainer: Noa Body <noabody@archlinux.org>
+
+pkgname=pcloud-drive
+pkgver=1.9.9
+pkgrel=1
+pkgdesc='pCloud drive. Electron edition.'
+arch=(x86_64)
+url=https://www.pcloud.com
+license=(custom)
+depends=(alsa-lib dbus-glib dbus-x11 fuse2 gtk3 hicolor-icon-theme libdbusmenu-gtk2 libindicator-gtk2 libxss nss zlib)
+options=(!emptydirs)
+replaces=(pcloud-git pcloud)
+_shortname=pcloud
+_appimage="$pkgname-$pkgver-$pkgrel.AppImage"
+_api_code=XZWTVkVZQM0GNXA4hrFGPkefzUUWVLKOpPIX
+_api_response="$(curl -s https://api.pcloud.com/getpublinkdownload?code="$_api_code")"
+_dlhost="$(echo $_api_response | grep -E -o '[a-zA-Z0-9\-]+\.pcloud\.com' | head -n 2 | sort -R | head -n 1)"
+_dlpath="$(echo $_api_response | grep -E -o "\"path\":\s{0,1}\".+\"" | cut -d '"' -f 4 | tr -d '\\')"
+source=("$_appimage"::https://"$_dlhost$_dlpath")
+sha256sums=('cd7e22f1615352b2481a36a07851862152f3fcffafb930d6fd5e62e8b9b7f3b0')
+
+prepare() {
+    chmod +x "$_appimage"
+    ./"$_appimage" --appimage-extract
+    find squashfs-root -type f -regextype posix-extended ! -iregex '.*/(AppRun|pcloud)' -exec chmod 644 "{}" +
+    find squashfs-root -type d -exec chmod 755 "{}" +
+    perl -pi -e 's|! -z \"\$DESKTOPINTEGRATION\"|1 = 1|gi' squashfs-root/AppRun
+    perl -pi -e "s|(appPath =) arg.appPath|\1 \"/usr/bin/$_shortname\"|gi ; s|(startup script)| \1|gi" squashfs-root/resources/app/node_modules/auto-launch/dist/AutoLaunchLinux.js
+    rm -f squashfs-root/pcloud.png
+}
+
+package() {
+    install -dm 755 "$pkgdir"/usr/{bin,share/{applications,icons}}
+    cd "$srcdir"
+    mv squashfs-root/usr/share/icons/hicolor "$pkgdir"/usr/share/icons/
+    perl -pi -e "s|(Exec)=AppRun|\1=/usr/bin/$_shortname|gi ; s|(Name)=pcloud|\1=pCloud|gi" squashfs-root/"$_shortname".desktop
+    mv squashfs-root/"$_shortname".desktop "$pkgdir"/usr/share/applications/
+    mv squashfs-root "$pkgdir"/usr/share/"$pkgname"
+
+    ln -rsf "$pkgdir"/usr/share/"$pkgname"/AppRun "$pkgdir"/usr/bin/pcloud
+}
