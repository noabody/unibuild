# Maintainer: Noa Body <noabody@archlinux.org>
# git clone --depth 1 https://aur.archlinux.org/google-chrome.git ~/Dev/google-chrome
# cp ~/Dev/unibuild/data/arch/chrome.pkgbuild ~/Dev/google-chrome/PKGBUILD
# cd ~/Dev/google-chrome && makepkg -sf

pkgname=google-chrome
pkgver=91.0.4472.114
pkgrel=1
pkgdesc="The popular and trusted web browser by Google (Stable Channel)"
arch=('x86_64')
url="https://www.google.com/chrome"
license=('custom:chrome')
depends=(alsa-lib gtk3 libcups libxss libxtst nss ttf-liberation xdg-utils)
optdepends=(
  'pipewire: WebRTC desktop sharing under Wayland'
  'kdialog: for file dialogs in KDE'
  'gnome-keyring: for storing passwords in GNOME keyring'
  'kwallet: for storing passwords in KWallet'
)
options=('!emptydirs' '!strip')
install=$pkgname.install
_channel=stable
source=("https://dl.google.com/linux/direct/google-chrome-${_channel}_current_amd64.deb"
  'eula_text.html'
  "google-chrome-$_channel.sh")
sha512sums=('SKIP'
            'a225555c06b7c32f9f2657004558e3f996c981481dbb0d3cd79b1d59fa3f05d591af88399422d3ab29d9446c103e98d567aeafe061d9550817ab6e7eb0498396'
            '43519ec81d008c9f949ef52b98a718473913e888d99ff6598083e30cd1f07376b5e58a7639fd309ee19056c18eac58cbf6a31bec6bfd2509ebce9fa9d6919743')

pkgver() {
  tar -xf control.tar.xz './control' --to-command="grep -Pio '(?<=Version: )[\d\.]+'"
}

package() {
  echo "  -> Extracting the data.tar.xz..."
  bsdtar -xf data.tar.xz -C "$pkgdir/"

  echo "  -> Moving stuff in place..."
  # Launcher
  install -m755 google-chrome-$_channel.sh "$pkgdir"/usr/bin/google-chrome-$_channel

  # Icons
  for i in 16x16 24x24 32x32 48x48 64x64 128x128 256x256; do
    install -Dm644 "$pkgdir"/opt/google/chrome/product_logo_${i/x*/}.png \
      "$pkgdir"/usr/share/icons/hicolor/$i/apps/google-chrome.png
  done

  # License
  install -Dm644 eula_text.html "$pkgdir"/usr/share/licenses/google-chrome/eula_text.html

  echo "  -> Fixing Chrome desktop entry..."
  sed -i \
    -e "/Exec=/i\StartupWMClass=Google-chrome" \
    -e "s/x-scheme-handler\/ftp;\?//g" \
    "$pkgdir"/usr/share/applications/google-chrome.desktop

  echo "  -> Removing Debian Cron job, duplicate product logos and menu directory..."
  rm -r \
    "$pkgdir"/etc/cron.daily/ \
    "$pkgdir"/opt/google/chrome/cron/ \
    "$pkgdir"/opt/google/chrome/product_logo_*.{png,xpm} \
    "$pkgdir"/usr/share/menu/
    rm -rf "$srcdir" "${srcdir%/*}"/google-chrome-stable_current_amd64.deb
}
