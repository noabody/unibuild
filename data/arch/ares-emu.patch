diff a/ares-emu/PKGBUILD b/ares-emu/PKGBUILD
--- a/ares-emu/PKGBUILD	1969-12-31 17:00:00.000000000 -0700
+++ b/ares-emu/PKGBUILD	1969-12-31 17:00:00.000000000 -0700
@@ -0,0 +1,41 @@
+# Maintainer: Noa Body <noabody@archlinux.org>
+
+pkgname=ares-emu
+_pkgname=ares
+pkgver=130.1.r119.g4524f9d18
+pkgrel=1
+pkgdesc='Multi-system emulator by Near with experimental Nintendo 64 and PlayStation support.'
+arch=(x86_64)
+url=https://ares-emu.net
+license=(ISC)
+depends=(gtk3 libao libgl libpulse libudev.so=1-64 libxv openal sdl2 vulkan-driver vulkan-icd-loader)
+makedepends=(mesa git)
+options=(!emptydirs)
+conflicts=("$_pkgname"-emu-git)
+source=(git+https://github.com/higan-emu/"$_pkgname".git)
+sha256sums=('SKIP')
+
+pkgver() {
+  cd "$_pkgname"
+  local _ver="$(grep -Pio "\svers.*\b.*\d+" "$_pkgname/$_pkgname/$_pkgname".hpp | grep -Pio '[\d\.]+')"
+  printf "%s.%s" "$_ver" "$(git describe --long --tags | sed 's/[^-]*-//;s/\([^-]*-g\)/r\1/;s/-/./g')"
+}
+
+prepare() {
+  cd "$_pkgname"
+  perl -pi -e 's|(= \{Path::)user(Data)|\1shared\2|gi ; s|(create\(\{Path::user)Data|\1Settings|gi ; s|(return \{Path::user)Data|\1Settings|gi' desktop-ui/desktop-ui.cpp
+  perl -0777 -pi -e 's|else ifeq.*root.*(?s).+?root(?-s).*\n||gi' {desktop-ui,mia}/GNUmakefile
+  perl -pi -e 's|(?<=-march=)native|x86-64|gi' desktop-ui/GNUmakefile
+  perl -pi -e 's|,--thinlto-cache-dir.*||gi' nall/GNUmakefile
+  make clean
+}
+
+build() {
+  make -C "$_pkgname"/desktop-ui hiro=gtk3 -j$(nproc)
+#  make -C "$_pkgname"/desktop-ui hiro=qt5 moc=/usr/bin/moc -j$(nproc)
+}
+
+package() {
+  make -C "$_pkgname"/desktop-ui hiro=gtk3 prefix="$pkgdir"/usr install
+#  make -C "$_pkgname"/desktop-ui hiro=qt5 moc=/usr/bin/moc prefix="$pkgdir"/usr install
+}
