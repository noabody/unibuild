diff a/dosbox-x/PKGBUILD b/dosbox-x/PKGBUILD
--- a/dosbox-x/PKGBUILD	1969-12-31 17:00:00.000000000 -0700
+++ b/dosbox-x/PKGBUILD	1969-12-31 17:00:00.000000000 -0700
@@ -0,0 +1,42 @@
+# Maintainer: Noa Body <noabody@archlinux.org>
+
+pkgname=dosbox-x
+pkgver=20230501.r76.g0ae8523
+pkgrel=1
+epoch=1
+pkgdesc='Fork of DOSBox, an x86 emulator with DOS.'
+arch=(x86_64)
+url=http://dosbox-x.com
+license=(GPL2)
+depends=(libxkbfile libxrandr ffmpeg libslirp tslib)
+makedepends=(git glu libpcap )
+optdepends=('openglide-git: Third-party 3dfx Glide API support'
+ 'libpcap: Allows DosBox-X to connect to a network and to the internet'
+ 'libslirp: Allows DosBox-X to connect to the internet rootless solution')
+source=("$pkgname"::git+https://github.com/joncampbell123/"$pkgname".git)
+provides=("$pkgname")
+replaces=("$pkgname"-git "$pkgname"-fast-bios-git "$pkgname"-sdl2)
+conflicts=("$pkgname"-git "$pkgname"-fast-bios-git "$pkgname"-sdl2)
+sha256sums=('SKIP')
+
+pkgver() {
+  cd "$pkgname"
+  git describe --long --abbrev=7 --tags | sed 's/.*-v//;s/\.//g;s/\([^-]*-g\)/r\1/;s/-/./g'
+}
+
+prepare() {
+  cd "$pkgname"
+  sed -i -e 's/-j3/-j$(nproc)/g' build
+  test ! -f "Makefile" || make distclean
+}
+
+build() {
+  export CFLAGS="$CFLAGS -Wno-incompatible-pointer-types"
+#  export CXXFLAGS="${CXXFLAGS// -Werror=format-security}"
+  cd "$pkgname"
+  ./build
+}
+
+package() {
+  make -C "$pkgname" DESTDIR="$pkgdir" install
+}
